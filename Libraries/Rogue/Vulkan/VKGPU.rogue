module Vulkan

class VKGPU
  PROPERTIES
    context              : VKContext
    native_gpu           : VKNativeGPU
    index                : Int32
    name                 : String
    queue_families       : VKQueueFamily[]
    native "VkPhysicalDeviceFeatures         features;"
    native "VkPhysicalDeviceMemoryProperties memory_properties;"
    native "VkPhysicalDeviceProperties       properties;"

  METHODS
    method init( context, native_gpu, index )
      native @|vkGetPhysicalDeviceProperties( $this->native_gpu.value, &$this->properties );
              |vkGetPhysicalDeviceFeatures( $this->native_gpu.value, &$this->features );
              |vkGetPhysicalDeviceMemoryProperties( $this->native_gpu.value, &$this->memory_properties );

    method memory_type_index( possible_types:Int32, requirements_mask:VKMemoryProperty )->Int32
      local type_count = native( "VK_MAX_MEMORY_TYPES" )->Int32
      forEach (i in 0..<type_count)
        local properties = native( "$this->memory_properties.memoryTypes[$i].propertyFlags" )->Int32
        if ((possible_types & (1:<<:i))? and ((properties & requirements_mask.value) == requirements_mask.value))
          return i
        endIf
      endForEach

      log.println "[Vulkan] ERROR: Required memory type not found."
      return 0

    method name->String
      if (not @name)
        native @|$name = RogueString_create( (const char*) &($this->properties.deviceName) );
      endIf
      return @name

    method queue_families->VKQueueFamily[]
      if (@queue_families) return @queue_families

      local result = VKQueueFamily[]
      native
        @|uint32_t count = 0;
         |vkGetPhysicalDeviceQueueFamilyProperties( $this->native_gpu.value, &count, NULL );
         |if (count && count < 64)
         |{
         |  VkQueueFamilyProperties properties[64];
         |  int i;
         |  vkGetPhysicalDeviceQueueFamilyProperties( $this->native_gpu.value, &count, properties );
         |  for (i=0; i<count; ++i)
         |  {
              local family : VKQueueFamily
      native
        @|    $family.context    = $context;
         |    $family.gpu        = $this;
         |    $family.index      = (RogueInt32) i;
         |    $family.properties = properties[i];
      result.add( family );
      native
        @|  }
         |}

         @queue_families = result
         return @queue_families

    method to->String
      return "$: $ ($)"(index,name,type)

    method type->VKDeviceType
      local result : Int32
      native @|$result = (RogueInt32) $this->properties.deviceType;
      return VKDeviceType( result )
endClass
