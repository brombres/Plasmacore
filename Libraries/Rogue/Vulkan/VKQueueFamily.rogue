module Vulkan

class VKQueueFamily [compound]
  PROPERTIES
    context             : VKContext
    gpu                 : VKGPU
    index               : Int32
    supports_presenting : Logical?
    native "VkQueueFamilyProperties properties;"

  METHODS
    method supports_graphics->Logical
      return native("($this.properties.queueFlags & VK_QUEUE_GRAPHICS_BIT)")->Logical

    method supports_presenting->Logical
      if (@supports_presenting) return @supports_presenting.value

      local result : Logical
      native
        @|VkBool32 supports_presenting;
         |$context->vkGetPhysicalDeviceSurfaceSupportKHR.value(
         |  $gpu->native_gpu.value,
         |  $this.index,
         |  $context->surface->native_surface.value,
         |  &supports_presenting
         |);
         |$result = (RogueLogical) supports_presenting;

      @supports_presenting = result
      return result

    method to->String
      return "($ supports_graphics:$ supports_presenting:$)"(index,supports_graphics,supports_presenting)

endClass

